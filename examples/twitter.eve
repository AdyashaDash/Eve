~~~
commit @browser
   [#div #twitter text:"twitter" style:[border:"10px"]]
~~~

start the twitter login if the user presses the button
~~~
  search @event @browser
      [#click element:[#twitter]]
  search
      [#time timestamp]
  commit @server
      [#login service:"twitter" timestamp]
  commit @browser
      [#div text:"clack"]
~~~

server starts the login by tickling twitter
~~~
  search @server
         k = [#login service timestamp]
  search @init
         [tag:service consumer-key consumer-secret]
  commit
         [#oauth-request
          method:"GET"
          key:"{{urlencode[text:consumer-secret]}}&"
          url:"https://api.twitter.com/oauth/request_token"
          fields:
            [oauth_callback:"https://aws.kodowa.com:8080"
             oauth_consumer_key:consumer-key
             oauth_nonce: "abcdefghijklmnopqrstuvwxyz012385"
             oauth_timestamp: floor[value: timestamp / 1000.0]
             oauth_signature_method: "HMAC-SHA1"
             oauth_version: "1.0"]]
~~~

sign the oauth request and add the signature field
~~~
  search
     r = [#oauth-request method fields url key]
     not(r.fields.oauth_signature)
     lookup[record:r.fields attribute value]
     index = sort[value:attribute given:attribute]
     sig = "{{method}}&{{urlencode[text:url]}}&{{urlencode[text:join[token:"{{attribute}}={{urlencode[text:value]}}" with:"&" index given:attribute]]}}"
   commit
      fields.oauth_signature := sha1hmac[text:sig key]
~~~

add the header and send the request
~~~
  search k = [#oauth-request method url fields:[oauth_signature]]
     lookup[record:k.fields attribute value]
     index = sort[value:attribute given:attribute]
     oauth-header = "OAuth {{join[token:"{{attribute}}=\"{{urlencode[text:value]}}\"" with:", " index given:attribute]}}"
  commit @http
     [#request #oauth-request url headers:[Authorization : oauth-header]]
~~~

get request status
~~~
  search @http
      [#request #oauth-request response]
      k = split[text:response.body by:"&"]
      attribute = split[text:k by:"=" index:1]
      value = split[text:k by:"=" index:2]
  commit
      z = [#token]
      lookup[record:z attribute value]
commit @browser-session
      p = [#zikky]
      lookup[record:p attribute value]
~~~

send login to browser
~~~
search
   [#token oauth_token]
commit @browser-session
   [#trag url:"https://api.twitter.com/oauth/authenticate?oauth_token={{oauth_token}}"]
~~~


print a debugging div
~~~
  search @browser-session
         z = [#zikky]
        lookup[record:z attribute value]
  commit @browser
     [#div text:"{{attribute}} {{value}}"]
~~~

build the login button
~~~
  search @browser-session
        [#trag url]
  commit @browser
        [#a target:"_blank" href:url text:"login!"]
~~~
