send the stripe test key to the browser
note that the tags have to be different here, to stop the browser
from forwarding the stripe record to itself indefinately
```
search @init
    [#stripe url test-publishable-key]
commit @browser-session
    [#stripe-service url test-publishable-key]
```

```
search @browser-session
   s = [#stripe-service]
commit @browser
   [#div text:"stripy {{s.url}} {{base64[text:s.url]}}"]
```

```
search @browser-session
   s = [#stripe-service]
bind @browser
   style = [box-shadow: "inset 0 -1px 5px 0 rgba(0, 0, 0, 0.2)" width:"300px"]
   [#div style:[display:"flex" display:"-webkit-box" flex-direction:"column"]
         children:
         // cant get these guys to obey explicit size or maxlength in characters
         [#input #ccn placeholder: "Credit Card Number" style]
         [#input #exp-month placeholder: "Expiration Month" style]
         [#input #exp-year placeholder: "Expiration Year" style]
         [#input #cvc placeholder: "CVC" style]
         [#div #send-cc-info text:"send!"]]
```

```
search @browser @event @session
   [#click element:[#send-cc-info]]
   [#input #ccn value:ccn]
   [#input #exp-month value:exp-month]
   [#input #exp-year value:exp-year]
   [#input #cvc value:cvc]
   data = "card[number]={{ccn}}&card[exp_month]={{exp-month}}&card[exp_year]={{exp-year}}&card[cvc]={{cvc}}"
search @browser-session
   s = [#stripe-service]
commit @http
   [#request #api
       method:"POST"
       url:"{{s.url}}/v1/tokens"
       body: data
       headers:[Authorization:"Bearer {{s.test-publishable-key}}"
                Content-type: "application/x-www-form-urlencoded"
                Access-Control-Allow-Origin: "localhost:8080"
                Stripe-Version: "2016-07-06"]]
commit @browser
   [#div text:"sending {{s.url}}/v1/token {{data}}"]
```


browser gets the card token back from stripe, and relays it to the server for charging
TODO - http.ts code needs to handle and propagate failures better
```
search @http
   r = [#request #api response:[json]]
commit @browser
   [#div text:"got response {{json.id}}"]
// construct the stripe object
commit @server
   [#charge source:json.id]
```

server gets stripe cc token and attempts to process the charge
```
search @init
   stripe = [#stripe]
search @server
   charge = [#charge]
   // examine the encoding for strings containing spaces...i dont know whats going
   // on with url encoding
   data = "amount=100&source={{charge.source}}&currency=usd&description=flosso"
commit @http
   [#request #charge-request
             charge
             method:"POST"
             headers:[Authorization:"Basic {{base64[text:"{{stripe.test-secret-key}}:"]}}"
                      Content-type: "application/x-www-form-urlencoded"]
             url:"{{stripe.url}}/v1/charges"
             body:data]
```

server gets response from stripe
```
search @http
   [#request response]
   lookup[record:response attribute value]
commit @browser-session
   [#charge status:"{{attribute}} {{value}}"]
```

browser gets response from server
```
search @browser-session
   [#charge status]
commit @browser
   [#div text:"status: {{status}}"]
```